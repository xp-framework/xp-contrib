/**
 * This file is part of the XP-Framework
 *
 * Aftermath
 * Copyright (c) 2011, XP-Framework Team
 */
package net.xp_forge.aftermath.truth.php.comment;

import net.xp_forge.aftermath.Deed;
import net.xp_forge.aftermath.truth.PhpTruth;

import native standard.strpos;
import native standard.stripos;

/**
 * Checks for TODO and FIXME in comments
 *
 * @test xp://unittest.net.xp_forge.aftermath.truth.php.comment.TodoPhpTruthTest
 */
[
  @severity('major'),
  @name('TODO and FIXME'),
  @message('Comment refers to an unfinished task, a hack or some ugly code that needs attention')
]
public class TodoPhpTruth extends PhpTruth {
  public static string[] $keywords= [
    'TODO', 'FIXME', 'XXX', 'TBD', 'HACK', 'UGLY'
  ];

  /**
   * @inheritDoc
   *
   */
  public bool forbids(Deed $deed) {
    if ('T_COMMENT' !== $deed.name && 'T_DOC_COMMENT' !== $deed.name) return false;

    // Check all keywords
    foreach ($keyword in self::$keywords) {
      if (
        false !== strpos($deed.description, $keyword) ||
        false !== stripos($deed.description, '@' ~ $keyword ~ ' ')
      ) return true;
    }

    // Keywords free
    return false;
  }
}
