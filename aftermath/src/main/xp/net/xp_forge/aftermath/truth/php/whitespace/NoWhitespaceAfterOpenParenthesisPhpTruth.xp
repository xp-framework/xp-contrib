/**
 * This file is part of the XP-Framework
 *
 * Aftermath
 * Copyright (c) 2011, XP-Framework Team
 */
package net.xp_forge.aftermath.truth.php.whitespace;

import net.xp_forge.aftermath.Deed;
import net.xp_forge.aftermath.truth.PhpTruth;

import native standard.strpos;

/**
 * Enforces that there is no whitespace after the opening parenthesis '('
 *
 * Exceptions:
 * - Newline after whitespace ( \n <- Trailing whitespace
 * - Comment after whitespace ( // <- Comment
 *
 * @test xp://unittest.net.xp_forge.aftermath.truth.php.whitespace.NoWhitespaceAfterOpenParenthesisPhpTruthTest
 */
[
  @severity('minor'),
  @name('No whitespace after open parenthesis'),
  @message('There is whitespace after the open parenthesis')
]
public class NoWhitespaceAfterOpenParenthesisPhpTruth extends PhpTruth {

  /**
   * @inheritDoc
   *
   */
  public bool forbids(Deed $deed) {

    // Check previous Deed is '('
    if (
      'T_WHITESPACE' !== $deed.name ||
      null === $deed.previous ||
      'A_OPEN_PARENTHESIS' !== $deed.previous.name
    ) return false;

    // Check next Deed after WHITESPACE is NEWLINE or COMMENT
    if (
      null !== $deed.next &&
      ('A_NEWLINE' === $deed.next.name || 'T_COMMENT' === $deed.next.name)
    ) return false;

    // Got ya
    return true;
  }
}
